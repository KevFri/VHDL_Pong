library ieee;

use ieee.std_logic_1164.all;
use ieee.numeric_std.all;

use work.Color.all;




entity Image_Generator is

generic(
	Background_Color : std_logic_vector(3 downto 0) := white					--Hintergrundfarbe
);
port(
	Reset: in std_logic := '0';														--asynchroner Reset, High aktiv
	Clk: in std_logic   := '0';														--50MHz Clock
	-------------------------------------------------------------------------------------------------------------------
	ImageGen_En: in std_logic := '0';												--Enable für Image Generator, High aktiv
	-------------------------------------------------------------------------------------------------------------------
	VGA_Paddle1_Pixel_En: in std_logic   := '0';									--Enable für aktuelles Pixel, High aktiv
	VGA_Paddle2_Pixel_En: in std_logic   := '0';									--Enable für aktuelles Pixel, High aktiv
	VGA_Ball_Pixel_En: in std_logic      := '0';									--Enable für aktuelles Pixel, High aktiv
	VGA_Spielfeld_Pixel_En: in std_logic := '0'; 								--Enable für aktuelles Pixel, High aktiv
	-------------------------------------------------------------------------------------------------------------------
	VGA_Paddle1_Pixel_Color: in std_logic_vector(3 downto 0)  := x"0";	--Farbe für aktuelles Pixel
	VGA_Paddle2_Pixel_Color: in std_logic_vector(3 downto 0)  := x"0";	--Farbe für aktuelles Pixel
	VGA_Ball_Pixel_Color: in std_logic_vector(3 downto 0)     := x"0";	--Farbe für aktuelles Pixel
	VGA_Spielfeld_Pixel_Color: in std_logic_vector(3 downto 0):= x"0";	--Farbe für aktuelles Pixel
	-------------------------------------------------------------------------------------------------------------------
	VGA_R: out std_logic_vector(7 downto 0):= x"00";							--Farbausgabe Rot aus LUT für DAC
	VGA_G: out std_logic_vector(7 downto 0):= x"00";							--Farbausgabe Grün aus LUT für DAC
	VGA_B: out std_logic_vector(7 downto 0):= x"00" 							--Farbausgabe Blau aus LUT für DAC
	);
begin	

end entity Image_Generator;

architecture behaviour of Image_Generator is
	--internes Farbsignal, zum austausch zwischen den Prozessen
	signal VGA_Pixel_Color: std_logic_vector(3 downto 0) := white;

begin

	PixelColorProzess: process(Clk,Reset)
	begin	
		if(Reset = '1') then
			VGA_Pixel_Color  <= (others => '0');
		elsif(rising_edge(Clk)) then
		
				if(ImageGen_En) then --Auswertung der enable signale von Ball, Paddle1, Paddle2 und Spielsteuerung
					if( VGA_Ball_Pixel_En ) then
						VGA_Pixel_Color <= VGA_Ball_Pixel_Color;
					elsif (VGA_Paddle1_Pixel_En) then
						VGA_Pixel_Color <= VGA_Paddle1_Pixel_Color;
					elsif (VGA_Paddle2_Pixel_En) then
						VGA_Pixel_Color <= VGA_Paddle2_Pixel_Color;
					elsif (VGA_Spielfeld_Pixel_En) then
						VGA_Pixel_Color <= VGA_Spielfeld_Pixel_Color;
					else
						VGA_Pixel_Color <= Background_Color; --Hintergrundfarbe wird ausgegeben, wenn keine andere Instanz ein Pixel zeichnet
					end if;
					
				else  --setze alles auf Schwarz, wenn kein Enable vorhanden ist (außerhalb des "aktiven Videosignals")
					VGA_Pixel_Color  <= (others => '0');
				end if;	
		end if;
	end process;
	
	RGB_Prozess: process(VGA_Pixel_Color) --wertet das interne Signal VGA_Pixel_Color aus und setzt dieses in RGB Werte um
	begin
					case VGA_Pixel_Color is
						when black   			=> 	VGA_R <= 8D"0";
															VGA_G <= 8D"0";
															VGA_B <= 8D"0";
						when blue   			=> 	VGA_R <= 8D"0";
															VGA_G <= 8D"0";
															VGA_B <= 8D"170";				
						when green   			=> 	VGA_R <= 8D"0";
															VGA_G <= 8D"170"; 
															VGA_B <= 8D"0";
						when cyan   			=> 	VGA_R <= 8D"0";
															VGA_G <= 8D"170"; 
															VGA_B <= 8D"170"; 
						when red   				=> 	VGA_R <= 8D"170"; 
															VGA_G <= 8D"0"; 
															VGA_B <= 8D"0";
						when magenta   		=> 	VGA_R <= 8D"170"; 
															VGA_G <= 8D"0"; 
															VGA_B <= 8D"170";
						when brown   			=> 	VGA_R <= 8D"170"; 
															VGA_G <= 8D"85"; 
															VGA_B <= 8D"0"; 
						when gray   			=> 	VGA_R <= 8D"170"; 
															VGA_G <= 8D"170";
															VGA_B <= 8D"170"; 
						when dark_gray   		=> 	VGA_R <= 8D"85"; 
															VGA_G <= 8D"85";
															VGA_B <= 8D"85";
						when ligth_blue   	=>		VGA_R <= 8D"85"; 
															VGA_G <= 8D"85";
															VGA_B <= 8D"255";
						when ligth_green   	=> 	VGA_R <= 8D"85"; 
															VGA_G <= 8D"255";
															VGA_B <= 8D"85"; 
						when ligth_cyan   	=> 	VGA_R <= 8D"85"; 
															VGA_G <= 8D"255";
															VGA_B <= 8D"255";
						when ligth_red  		=> 	VGA_R <= 8D"255"; 
															VGA_G <= 8D"85";
															VGA_B <= 8D"85"; 
						when ligth_magenta   => 	VGA_R <= 8D"255"; 
															VGA_G <= 8D"85";
															VGA_B <= 8D"255"; 
						when yellow   			=> 	VGA_R <= 8D"255"; 
															VGA_G <= 8D"255";
															VGA_B <= 8D"85";
						when white   			=> 	VGA_R <= 8D"255"; 
															VGA_G <= 8D"255";
															VGA_B <= 8D"255"; 
						when others 			=> 	VGA_R <= 8D"0";
															VGA_G <= 8D"0";
															VGA_B <= 8D"0";										
				end case;
		end process;
		--Color Table
		--CGA	RGB	Farbe
		--0x0	0,0,0	black
		--0x1	0,0,170	blue
		--0x2	0,170,0	green
		--0x3	0,170,170	cyan
		--0x4	170,0,0	red
		--0x5	170,0,170	magenta
		--0x6	170,85,0	brown
		--0x7	170,170,170	gray
		--0x8	85,85,85	dark gray
		--0x9	85,85,255	bright blue
		--0xA	85,255,85	bright green
		--0xB	85,255,255	bright cyan
		--0xC	255,85,85	bright red
		--0xD	255,85,255	bright magenta
		--0xE	255,255,85	yellow
		--0xF	255,255,255	white
end architecture behaviour;